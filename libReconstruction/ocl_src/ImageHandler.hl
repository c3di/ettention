#ifndef IMAGEHANDLER_HL
#define IMAGEHANDLER_HL
#include "Math.hl"

typedef struct
{
    int3 resolution;
    int addressingMode;
} BufferImageProperties;

inline float LinearInterpolation(__global const float* data, int dataSize, float coord)
{
    float baseCoord;
    float t = fract(coord - 0.5f, &baseCoord);
    int coordBX = (int)baseCoord;
    int coordNX = coordBX + 1;
    float valBX = coordBX >= 0 && coordBX < dataSize ? data[coordBX] : 0.0f;
    float valNX = coordNX >= 0 && coordNX < dataSize ? data[coordNX] : 0.0f;
    return mix(valBX, valNX, t);
}

inline float BilinearInterpolation(__global const float* data, int2 dataSize, float2 coord)
{
    float baseCoord;
    float t = fract(coord.y - 0.5f, &baseCoord);
    int coordBY = convert_int(baseCoord);
    int coordNY = coordBY + 1;
    float valBY = coordBY >= 0 && coordBY < dataSize.y ? LinearInterpolation(data + coordBY * dataSize.x, dataSize.x, coord.x) : 0.0f;
    float valNY = coordNY >= 0 && coordNY < dataSize.y ? LinearInterpolation(data + coordNY * dataSize.x, dataSize.x, coord.x) : 0.0f;
    return mix(valBY, valNY, t);
}

inline float TrilinearInterpolation(__global const float* data, int3 dataSize, float3 coord)
{
    float baseCoord;
    float t = fract(coord.z - 0.5f, &baseCoord);
    int coordBZ = convert_int(baseCoord);
    int coordNZ = coordBZ + 1;
    float valBZ = coordBZ >= 0 && coordBZ < dataSize.z ? BilinearInterpolation(data + coordBZ * dataSize.x * dataSize.y, dataSize.xy, coord.xy) : 0.0f;
    float valNZ = coordNZ >= 0 && coordNZ < dataSize.z ? BilinearInterpolation(data + coordNZ * dataSize.x * dataSize.y, dataSize.xy, coord.xy) : 0.0f;
    return mix(valBZ, valNZ, t);
}

#ifdef IMAGE_SUPPORT

#define MEMORY2D(name) __read_only image2d_t name, sampler_t name##_sp
#define MEMORY3D(name) __read_only image3d_t name, sampler_t name##_sp
#define MEMORY2D_PARAMS(name) name, name##_sp
#define MEMORY3D_PARAMS(name) name, name##_sp
#define READ_MEMORY2D(name, coord) read_imagef(name, name##_sp, coord).x
#define READ_MEMORY3D(name, coord) read_imagef(name, name##_sp, coord).x
#define MEMORY2D_SIZE(name) get_image_dim(name)
#define MEMORY3D_SIZE(name) get_image_dim(name)

#else

#define MEMORY2D(name) __global const float* name, __global const BufferImageProperties* name##_props
#define MEMORY3D(name) __global const float* name, __global const BufferImageProperties* name##_props
#define MEMORY2D_PARAMS(name) name, name##_props
#define MEMORY3D_PARAMS(name) name, name##_props
#define READ_MEMORY2D(name, coord) BilinearInterpolation(name, name##_props->resolution, coord)
#define READ_MEMORY3D(name, coord) TrilinearInterpolation(name, name##_props->resolution, coord.xyz)
#define MEMORY2D_SIZE(name) name##_props->resolution
#define MEMORY3D_SIZE(name) name##_props->resolution

#endif

#endif